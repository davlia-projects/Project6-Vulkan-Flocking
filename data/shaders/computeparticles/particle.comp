#version 450

#extension GL_ARB_separate_shader_objects : enable
#extension GL_ARB_shading_language_420pack : enable

struct Particle
{
	vec2 pos;
	vec2 vel;
};

// LOOK: These bindings correspond to the DescriptorSetLayouts and
// the DescriptorSets from prepareCompute()!

// Binding 0 : Particle storage buffer (read)
layout(std140, binding = 0) buffer ParticlesA
{
   Particle particlesA[ ];
};

// Binding 1 : Particle storage buffer (write)
layout(std140, binding = 1) buffer ParticlesB
{
   Particle particlesB[ ];
};

layout (local_size_x = 16, local_size_y = 16) in;

// LOOK: rule weights and distances, as well as particle count, based off uniforms.
// The deltaT here has to be updated every frame to account for changes in
// frame rate.
layout (binding = 2) uniform UBO
{
	float deltaT;
	float rule1Distance;
	float rule2Distance;
	float rule3Distance;
	float rule1Scale;
	float rule2Scale;
	float rule3Scale;
	int particleCount;
} ubo;

vec2 computeVelocity(uint index) {
	vec2 center = vec2(0.0f, 0.0f);
	vec2 separate = vec2(0.0f, 0.0f);
	vec2 cohesion = vec2(0.0f, 0.0f);
	vec2 tVel = vec2(0.0f, 0.0f);
	int neighborCount = 0;
	int test = 0;
	for (int i = 0; i < ubo.particleCount; i++) {
		if (i == index) {
			continue;
		}

		float distance = distance(particlesA[index].pos, particlesA[i].pos);
		if (distance < ubo.rule1Distance) {
			center += particlesA[i].pos;
			neighborCount++;
		}
		if (distance < ubo.rule2Distance) {
			separate += particlesA[index].pos - particlesA[i].pos;
		}
		if (distance < ubo.rule3Distance) {
			cohesion += particlesA[i].vel;
			test++;
		}
	}
	if (neighborCount > 0) {
		center /= neighborCount;
		tVel += (center - particlesA[index].pos) * ubo.rule1Scale;
	}
	tVel += separate * ubo.rule2Scale;
	if (test > 0) {
		tVel += cohesion * ubo.rule3Scale;
	}
	return tVel;
}

void main()
{

    // Current SSBO index
    uint index = gl_GlobalInvocationID.x;
	// Don't try to write beyond particle count
    if (index >= ubo.particleCount)
		return;

    vec2 vVel = particlesA[index].vel.xy;
	vec2 vPos = particlesA[index].pos.xy;

	vVel = computeVelocity(index);
	// clamp velocity for a more pleasing simulation.
	vVel = normalize(vVel) * clamp(length(vVel), 0.0, 0.1);

	// kinematic update
	vPos += vVel * ubo.deltaT;

    // Wrap around boundary
	if (vPos.x < -1.0) vPos.x = 1.0;
	if (vPos.x > 1.0) vPos.x = -1.0;
	if (vPos.y < -1.0) vPos.y = 1.0;
	if (vPos.y > 1.0) vPos.y = -1.0;

    particlesB[index].pos.xy = vPos;

    // Write back
    particlesB[index].vel.xy = vVel;
}
